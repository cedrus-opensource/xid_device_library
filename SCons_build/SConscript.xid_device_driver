#!/usr/bin/env python

import CedrusBoost
import CedrusSConsDefaultEnvironments
import CedrusSConsHelperFunctions

import os
import sys

Import('env')

# this line is a keeper:
boost = CedrusBoost.CedrusBoostSettings(env)
boost.need_boost_filesystem()
boost.need_boost_system()

project_target_name = 'xid_device_driver'

prefix = ''

if env.has_key( 'THE_CEDRUS_XIDON_BUILD' ):
    prefix = '$OBJ_ROOT/xid_device_library/'

inputs = [
    prefix + 'xid_device_driver/xid_con_t.cpp',
    prefix + 'xid_device_driver/xid_con_test_only.cpp',
    prefix + 'xid_device_driver/xid_device_config_t.cpp',
    prefix + 'xid_device_driver/response_mgr.cpp',
    prefix + 'xid_device_driver/xid_glossary.cpp',
    prefix + 'xid_device_driver/xid_device_scanner_t.cpp',
    prefix + 'xid_device_driver/base_device_t.cpp',
    prefix + 'xid_device_driver/device_factory.cpp',
    prefix + 'xid_device_driver/xid_device_t.cpp',
    prefix + 'xid_device_driver/xid2_device.cpp',
    prefix + 'xid_device_driver/stim_tracker_t.cpp',
]

defines = [ 'CEDRUS_XID_MAKEDLL' ]
include_path = [ prefix + 'scons_helpers/cpp_src/' ]
cxxflags = []
frameworks = []
if sys.platform == 'win32':
    lib_dependencies = ['ftd2xx']
    env.Install( str( env.subst('$STAGING_DIR')), env.subst('ftd2xx.dll') )
if sys.platform == 'darwin':
    lib_dependencies = ['ftd2xx.1.2.2']
    env.Install( str( env.subst('$STAGING_DIR')), env.subst('libftd2xx.1.2.2.dylib') )
lib_path = ['.']
linkflags = []

if sys.platform == 'win32':
    linkflags = [ '/SUBSYSTEM:WINDOWS',  '/VERSION:4.5', '/MANIFEST', '/INCREMENTAL', ]

elif sys.platform == 'darwin':
    frameworks = ['CoreFoundation', 'IOKit']

staged_binary = CedrusSConsHelperFunctions.DeclareSConsSharedLibraryBuild(
    env,
    project_target_name,
    inputs,
    defines,
    include_path,
    cxxflags,
    frameworks,
    lib_dependencies,
    lib_path,
    linkflags
)
